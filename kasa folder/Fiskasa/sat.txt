unit MainU;

interface

uses
  Windows, Messages, SysUtils, Classes, Graphics,
  Controls, Forms, Dialogs,
  StdCtrls,mmsystem, ExtCtrls, jpeg, ComCtrls,
  Buttons, ImgList, ShellApi, XPMan;

type
  TForm1 = class(TForm)
    Timer1: TTimer;
    Timer2: TTimer;
    Bg: TImage;
    ExitBtn: TImage;
    AddBtn: TImage;
    EditBtn: TImage;
    DelBtn: TImage;
    PrintBtn: TImage;
    RemBtn: TImage;
    HelpBtn: TImage;
    AboutBtn: TImage;
    Led: TImage;
    XBtn: TImage;
    Edit7: TEdit;
    NumLabel: TLabel;
    TotalNumb: TLabel;
    Label1: TLabel;
    Label2: TLabel;
    Label3: TLabel;
    Label4: TLabel;
    Label5: TLabel;
    EmailLabel: TLabel;
    EmailBtn: TImage;
    UrlLabel: TLabel;
    WebBtn: TImage;
    TimeLabel: TLabel;
    DateLabel: TLabel;
    AddressLabel: TLabel;
    NoteLabel: TLabel;
    Image1: TImage;
    RemLed: TImage;
    ComboBox1: TComboBox;
    PrevBtn: TImage;
    NextBtn: TImage;
    FirstBtn: TImage;
    LastBtn: TImage;
    XPManifest1: TXPManifest;

    procedure FormCreate(Sender: TObject);
    procedure Timer1Timer(Sender: TObject);
    procedure Edit7Change(Sender: TObject);
    procedure Timer2Timer(Sender: TObject);
    procedure AddBtnMouseDown(Sender: TObject; Button: TMouseButton;
      Shift: TShiftState; X, Y: Integer);
    procedure AddBtnMouseUp(Sender: TObject; Button: TMouseButton;
      Shift: TShiftState; X, Y: Integer);
    procedure AddBtnMouseMove(Sender: TObject; Shift: TShiftState; X,
      Y: Integer);
   
    procedure FormKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
    procedure TimeLabelMouseMove(Sender: TObject; Shift: TShiftState; X,
      Y: Integer);
    procedure DateLabelMouseMove(Sender: TObject; Shift: TShiftState; X,
      Y: Integer);
    procedure ComboBox1Click(Sender: TObject);
    procedure RemLedClick(Sender: TObject);
    procedure FormPaint(Sender: TObject);
    procedure FormMouseDown(Sender: TObject; Button: TMouseButton;
      Shift: TShiftState; X, Y: Integer);
    procedure Label1DblClick(Sender: TObject);
  private
    { Private-Deklarationen }

    Procedure DeleteRecord;
    Procedure UpdateFile;

  public
    { Public-Deklarationen }
    Procedure SeekRecord;
  end;

type
    QRecord = Record //this is the address book
    Title: String[10];
    FullName:string[40];
    Address:string[150];
    Hphone, HFax, Mobile,WPhone:string[20];
    Email,web,ImgFile:string[50];
    Note:string[100];
end;

var
  //BtnDn:TImage;
  Form1: TForm1;
  Entries: QRecord;
  QFile, TempFile: File of QRecord;
  RecNumb,FSize, Found:integer;

  QFileName,TempFileName,Path: string;

  Deleted, Edit, Pressed, LedOn:boolean;

implementation

uses AddU, PrintU, RemU, AboutU;

{$R *.DFM}
//---------------------------------
Function BSearch: integer;
var c,Midindex, MinIndex, MaxIndex:integer;
begin
     BSearch:=-1;
     MinIndex:=0;
     MaxIndex:=Form1.ComboBox1.Items.Count-1;
     While MaxIndex>=MinIndex do begin
           MidIndex:=(MinIndex+MaxIndex) div 2;
           C:=StrIComp(PChar(Form1.Edit7.text),PChar(copy(Form1.ComboBox1.Items[MidIndex],1,Length(Form1.Edit7.text))));
           if c>0 then
              MinIndex:=MidIndex+1
           else begin
                if c<0 then
                   MaxIndex:=MidIndex-1
                else begin
                    BSearch:=MidIndex;
                    Exit;
                end;
           end;
     end;
end;
//---------------------------------
Procedure TForm1.UpdateFile;
var n:integer;
begin
     //if not Deleted then exit;
     AssignFile(TempFile,TempFilename);
     Rewrite(TempFile);
     reset(QFile);
     for n:=0 to Fsize-1 do begin
         seek(QFile,n);
         Read(QFile, entries);
         If Entries.FullName<>'' then begin
            write(TempFile, Entries);
         end;
     end;
     closefile(TempFile);
     closefile(QFile);
     Erase(QFile);
     ReName(TempFile,QFileName);
     Reset(QFile);
     FSize:=FileSize(QFile);
     TotalNumb.caption:= inttostr(FSize);
end;
//---------------------------------
Procedure TForm1.SeekRecord;
var n:Integer;
begin
     // since some items may be deleted we have to search the whole file
     for n:=0 to FileSize(QFile)-1 do begin
         seek(Qfile,n);
         Read(QFile,Entries);
         if entries.FullName=ComboBox1.Items[RecNumb] then begin
            with Entries do begin
                 Label1.caption:=Title+' '+FullName;
                 AddressLabel.Caption:=Address;
                 Label2.caption:=Hphone;
                 Label3.Caption:=HFax;
                 Label4.Caption:=Mobile;
                 Label5.Caption:=WPhone;
                 EmailLabel.Caption:=Email;
                 UrlLabel.Caption:=Web;
                 If FileExists(Path+'Thumbs\'+imgFile) then
                    Image1.Picture.LoadFromFile(Path+'Thumbs\'+imgFile)
                 else Image1.Picture.Bitmap.LoadFromResourceName(hinstance,'NoPhoto');

                 NoteLabel.Caption:=Note;
                 ComboBox1.ItemIndex:=RecNumb;
                 Found:=n;
                 break;
            end;
         end;
     end;
end;
//---------------------------------
Procedure Tform1.DeleteRecord;
var n: integer;
begin
     if (ComboBox1.items.count<=0) then exit;
            //S:=PChar('Do you want to delete "' + ListBox1.Items[RecNumb] + '" ?');
            if Application.MessageBox(PChar('Do you want to delete "' +
            ComboBox1.Items[RecNumb] + '" ?'),'Delete Record',
               MB_YesNo + MB_IconQuestion + MB_DefButton2)=IDYes then begin
               Deleted:=True;
               for n:=0 to ComboBox1.Items.count-1 do begin
                   seek(Qfile,n);
                   Read(QFile,Entries);
                   if Entries.FullName= ComboBox1.Items[RecNumb] then begin
                      Entries.FullName:='';
                      seek(QFile,n);
                      Write(QFile,Entries);
                      If FileExists(Path+'Thumbs\'+Entries.ImgFile) then
                         DeleteFile(Path+'Thumbs\'+Entries.ImgFile);
                      break;
                   end;
               end;
               ComboBox1.items.Delete(RecNumb);
               if ComboBox1.Items.Count>0 then begin
                  RecNumb:=0;
                  ComboBox1.ItemIndex:=RecNumb;
                  SeekRecord;
               end else begin
                    Label1.Caption:='';
                    AddressLabel.Caption:='';
                    Label2.Caption:='';
                    Label3.Caption:='';
                    Label4.Caption:='';
                    Label5.Caption:='';
                    EmailLabel.Caption:='';
                    UrlLabel.Caption:='';
                    NoteLabel.Caption:='';
               end;
               UpdateFile;
            end;
end;
//---------------------------------
procedure TForm1.FormCreate(Sender: TObject);
var n:integer;
    Rgn1,Rgn2:THandle;
begin
     Form1.Width:=689; Form1.Height:=431;
     Rgn1:=CreateRoundRectRgn(177,0,698,50,12,12);
     Rgn2:=CreateRoundRectRgn(0,15,698,431-20,12,12);
     CombineRgn(Rgn1,Rgn1,Rgn2,Rgn_Or);
     Rgn2:=CreateRoundRectRgn(372,431-60,698,431,10,10);
     CombineRgn(Rgn1,Rgn1,Rgn2,Rgn_Or);
     SetWindowRgn(handle,Rgn1,true);
     DeleteObject(Rgn1);
     DeleteObject(Rgn2);
     // load buttons
     PrevBtn.Picture.Bitmap.LoadFromResourceID(hInstance,1);
     NextBtn.Picture.Bitmap.LoadFromResourceID(hInstance,3);
     FirstBtn.Picture.Bitmap.LoadFromResourceID(hInstance,5);
     LastBtn.Picture.Bitmap.LoadFromResourceID(hInstance,7);

     AddBtn.Picture.Bitmap.LoadFromResourceID(hInstance,10);
     EditBtn.Picture.Bitmap.LoadFromResourceID(hInstance,12);
     DelBtn.Picture.Bitmap.LoadFromResourceID(hInstance,14);
     RemBtn.Picture.Bitmap.LoadFromResourceID(hInstance,16);
     PrintBtn.Picture.Bitmap.LoadFromResourceID(hInstance,18);
     ExitBtn.Picture.Bitmap.LoadFromResourceID(hInstance,20);

     EmailBtn.Picture.Bitmap.LoadFromResourceID(hInstance,22);
     WebBtn.Picture.Bitmap.LoadFromResourceID(hInstance,24);

     RemLed.Picture.Bitmap.LoadFromResourceName(hInstance,'RemOff');
     HelpBtn.Picture.Bitmap.LoadFromResourceID(hInstance,26);
     AboutBtn.Picture.Bitmap.LoadFromResourceID(hInstance,28);
     XBtn.Picture.Bitmap.LoadFromResourceID(hInstance,30);

     Led.Picture.Bitmap.LoadFromResourceID(hInstance,32);

     TempFileName:=ExtractFilePath(application.exename)+'RecTemp.dat';
     AssignFile(TempFile,TempFileName);
     if FileExists(TempFileName) then Erase(TempFile);
     Path:= ExtractFilePath(application.exename);
     QFileName:=Path+'Records.dat';
     Assignfile(QFile, QFileName);
     if FileExists(QfileName) then begin
        reset(QFile);
        FSize:=FileSize(QFile);
        TotalNumb.caption:=inttostr(FSize);
        if FSize>0 then begin
           for n:=0 to Fsize-1 do begin
               seek(Qfile,n);
               Read(QFile,Entries);
               ComboBox1.Items.add(Entries.FullName);
           end;
           NumLabel.Caption:='1';
           ComboBox1.ItemIndex:=0;
           SeekRecord;
        end
     end else begin
          if Application.MessageBox('Data file is missing. Create new file?',
             'File Missing', MB_YesNo)=IDyes then begin
             ReWrite(QFile);
             Reset(QFile);
             TotalNumb.caption:='0';
             RecNumb:=0;
          end;
     end;
     RecNumb:=0;
     Pressed:=False;
     LedOn:=False;
     TimeLabel.caption:=FormatDateTime('hh:mm:ss',Time);
     DateLabel.Caption:=FormatDateTime('dddd dd. mmmm. yyy',Date);
     //Form1.DoubleBuffered:=True;
end;
//---------------------------------
procedure TForm1.Timer1Timer(Sender: TObject);
begin
     TimeLabel.caption:=FormatDateTime('hh:mm:ss',Time);
end;
//---------------------------------
procedure TForm1.Edit7Change(Sender: TObject);
var n:integer;
begin
     n:=BSearch();
     if n=-1 then beep
     else begin
          RecNumb:=n;
          ComboBox1.ItemIndex:=RecNumb;
          SeekRecord;
          NumLabel.Caption:=IntToStr(RecNumb+1);
     end;
end;
//---------------------------------
procedure TForm1.Timer2Timer(Sender: TObject);
begin
     if LedOn then
        Led.Picture.Bitmap.LoadFromResourceID(hInstance,Led.Tag+1)
     else
        Led.Picture.Bitmap.LoadFromResourceID(hInstance,Led.Tag);
     LedOn:= Not LedOn;
end;
//---------------------------------
procedure TForm1.AddBtnMouseDown(Sender: TObject; Button: TMouseButton;
  Shift: TShiftState; X, Y: Integer);
begin
     if Button<>mbLeft then exit;
     playsound('click',hInstance,snd_resource or snd_async);
     TImage(sender).Picture.Bitmap.LoadFromResourceID(hInstance,TImage(sender).tag+1);
     Pressed:=True;
end;
//---------------------------------
procedure TForm1.AddBtnMouseUp(Sender: TObject; Button: TMouseButton;
  Shift: TShiftState; X, Y: Integer);
//var S: Pchar;
//    n:integer;
begin
     TImage(sender).Picture.Bitmap.LoadFromResourceID(hInstance,TImage(sender).tag);
     If Not Pressed then exit;
     Pressed:=False;
     Application.ProcessMessages;
     case TImage(sender).Tag of
        1: begin
           if (ComboBox1.Items.Count<=0) Or (ComboBox1.ItemIndex<=0) then exit;
           ComboBox1.ItemIndex:=ComboBox1.ItemIndex-1;
           ComboBox1Click(self);
           end;
        3: begin
           if (ComboBox1.Items.Count<=0) Or (ComboBox1.ItemIndex>=ComboBox1.Items.Count) then exit;
           ComboBox1.ItemIndex:=ComboBox1.ItemIndex+1;
           ComboBox1Click(self);
           end;
        5: begin
           if (ComboBox1.Items.Count<=0) Or (ComboBox1.ItemIndex<=0) then exit;
           ComboBox1.ItemIndex:=0;
           ComboBox1Click(self);
           end;
        7: begin
           if (ComboBox1.Items.Count<=0) Or (ComboBox1.ItemIndex>=ComboBox1.Items.Count) then exit;
           ComboBox1.ItemIndex:=ComboBox1.Items.Count-1;
           ComboBox1Click(self);
           end;
        10: begin
            Timer2.Enabled:=True;
            Edit:=False;
            Application.CreateForm(TAddFrm, AddFrm);
            with AddFrm do begin
                 Caption:='Add Record';
                 Label10.Caption:='A D D   R E C O R D';
                 Edit1.text:='';
                 Memo1.Text:='';
                 Edit2.text:='';
                 Edit3.text:='';
                 Edit4.text:='';
                 Edit5.text:='';
                 Edit6.text:='';
                 Edit7.text:='';
                 Image1.Picture.Bitmap.LoadFromResourceName(hinstance,'NoPhoto');
                 Memo2.text:='';
            end;
            AddFrm.ShowModal;
            AddFrm.Release;
            AddFrm:=nil;
            Timer2.Enabled:=False;
            Led.Picture.Bitmap.LoadFromResourceID(hInstance,32);
            end;
        12: begin
            if (ComboBox1.Items.Count=0) then exit;
            Edit:=true;
            Timer2.Enabled:=True;
            Application.CreateForm(TAddFrm, AddFrm);
            SeekRecord;
            with AddFrm do begin
                 Caption:='Edit Record';
                 Label10.Caption:='E D I T   R E C O R D';
                 ComboBox1.ItemIndex:=ComboBox1.Items.IndexOf(Entries.Title);
                 Edit1.text:=Entries.FullName;
                 Memo1.Text:=Entries.Address;
                 Edit2.text:=Entries.Hphone;
                 Edit3.text:=Entries.HFax;
                 Edit4.text:=Entries.Mobile;
                 Edit5.text:=Entries.WPhone;
                 Edit6.text:=Entries.Email;
                 Edit7.text:=Entries.web;
                 if FileExists(Path+'Thumbs\'+Entries.ImgFile) then
                    Image1.Picture.LoadFromFile(Path+'Thumbs\'+Entries.ImgFile)
                 else Image1.Picture.Bitmap.LoadFromResourceName(hinstance,'NoPhoto');
                 
                 Memo2.text:=Entries.Note;
            end;
            AddFrm.ShowModal;
            AddFrm.Release;
            AddFrm:=Nil;
            Timer2.Enabled:=False;
            Led.Picture.Bitmap.LoadFromResourceID(hInstance,32);
            end;
        14: DeleteRecord;
        16: begin
            //Application.CreateForm(TRemFrm, RemFrm);
            RemFrm.Showmodal;
            //RemFrm.Release;
            end;
        18: begin
            if ComboBox1.Items.Count<=0 then exit;
            Application.CreateForm(TPrintFrm, PrintFrm);
            PrintFrm.ShowModal;
            PrintFrm.Release;
            PrintFrm:=nil;
            end;
        20, 30: begin
            if Deleted=true then UpdateFile;
            Form1.Close;
            Form1.Release;
            end;
        22: begin
            if EmailLabel.Caption='' then exit;
            ShellExecute(Application.Handle,'Open',
            Pchar('mailto:'+EmailLabel.Caption),nil,nil,sw_ShowNormal);
            end;
        24: begin
            if UrlLabel.Caption='' then exit;
            ShellExecute(Application.Handle,'Open',
            Pchar(UrlLabel.Caption),nil,nil,sw_ShowNormal);
            end;
        26: begin
            if Not FileExists(ExtractFilePath(application.exename)+'Address.hlp') then exit;
            Application.HelpFile:=ExtractFilePath(application.exename)+'Address.hlp';
            Application.HelpJump('Title');
            end;
        28: begin
            Application.CreateForm(TAboutFrm, AboutFrm);
            AboutFrm.ShowModal;
            AboutFrm.Release;
            end;
     end;
end;
//---------------------------------
procedure TForm1.AddBtnMouseMove(Sender: TObject; Shift: TShiftState; X,
  Y: Integer);
begin
     if Not (ssleft in shift) then exit;
     if (x<=0) or (x>=Timage(sender).width) or
        (y<=0) or (y>=TImage(sender).height) then begin
        if Pressed then TImage(sender).Picture.Bitmap.LoadFromResourceID(hInstance,TImage(sender).tag);
        Pressed:=False;
        end
     else if Not Pressed then begin
          TImage(sender).Picture.Bitmap.LoadFromResourceID(hInstance,TImage(sender).tag+1);
          Pressed:=True;
     end;
end;
//---------------------------------
procedure TForm1.FormKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
     if Key=46 then DeleteRecord; //Delete
end;
//---------------------------------
procedure TForm1.TimeLabelMouseMove(Sender: TObject; Shift: TShiftState; X,
  Y: Integer);
begin
     TimeLabel.Hint:=TimeLabel.Caption;
end;
//---------------------------------
procedure TForm1.DateLabelMouseMove(Sender: TObject; Shift: TShiftState; X,
  Y: Integer);
begin
     DateLabel.Hint:=DateLabel.Caption;
end;
//---------------------------------
procedure TForm1.ComboBox1Click(Sender: TObject);
begin
     if ComboBox1.Items.Count<=0 then exit;
     RecNumb:=ComboBox1.ItemIndex;
     NumLabel.Caption:=inttostr(RecNumb+1);
     SeekRecord;
end;
//---------------------------------
procedure TForm1.RemLedClick(Sender: TObject);
begin
     Pressed:=True;
     AddBtnMouseUp(RemBtn,MBLeft,[ssLeft],0,0);
end;
//---------------------------------
procedure TForm1.FormPaint(Sender: TObject);
begin
     Canvas.Draw(0,0,Bg.Picture.Graphic);
end;
//---------------------------------
procedure TForm1.FormMouseDown(Sender: TObject; Button: TMouseButton;
  Shift: TShiftState; X, Y: Integer);
begin
     ReleaseCapture;
     Form1.Perform(WM_SysCommand,$F012,0);  
end;
//---------------------------------
procedure TForm1.Label1DblClick(Sender: TObject);
begin
     Pressed:=True;
     AddBtnMouseUp(EditBtn,mbLeft,[ssLeft],0,0);
end;

end.